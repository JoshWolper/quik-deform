// guide found here: http://danostrov.com/2012/10/27/intro-to-aetemplates/

global proc AEQuikDeformNodeTemplate( string $nodeName )
{
    editorTemplate -beginScrollLayout;
 
	// --------------------------------------------
	//          Simulation attributes
	// --------------------------------------------
    editorTemplate -beginLayout "Simulation Attributes" -collapse 0;

        // custom float slider for timeStep
		editorTemplate -callCustom "timeSliderAttributeNew" "timeSliderAttributeUpdate" "timeStep";
		// custom int slider for solverIterations
		editorTemplate -callCustom "iterSliderAttributeNew" "iterSliderAttributeUpdate" "solverIterations";
        editorTemplate -label "Seconds" -addControl "secondsToSimulate";
		editorTemplate -label "Framerate" -addControl "frameRate";

    editorTemplate -endLayout;

	// --------------------------------------------
	//          Object attributes
	// --------------------------------------------
	editorTemplate -beginLayout "Object Attributes" -collapse 0;
        
        // custom float slider for mass
		editorTemplate -callCustom "massSliderAttributeNew" "massSliderAttributeUpdate" "mass";
		editorTemplate -label "Initial Object Velocity" - addControl "initialVelocity";
        // custom radio buttons for volumetric vs thin-shell
		editorTemplate -callCustom "volumetricRadioAttributeNew" "volumetricRadioAttributeUpdate" "volumetric";
		 // custom float slider for youngsModulus
		editorTemplate -callCustom "youngsModulusSliderAttributeNew" "youngsModulusSliderAttributeUpdate" "youngsModulus";
		// custom float slider for poissonRatio
		editorTemplate -callCustom "poissonRatioSliderAttributeNew" "poissonRatioSliderAttributeUpdate" "poissonRatio";

	editorTemplate -endLayout;

	// --------------------------------------------
	//          External Forces attributes
	// --------------------------------------------
	editorTemplate -beginLayout "External Forces" -collapse 0;

		editorTemplate -label "Enable Gravity" -addControl "doGravity";
		editorTemplate -label "Enable Wind" -addControl "doWind";
		editorTemplate -label "Wind Direction" -addControl "windDirection";
		// custom float slider for windMagnitude
		editorTemplate -callCustom "windMagnitudeSliderAttributeNew" "windMagnitudeSliderAttributeUpdate" "windMagnitude";
		editorTemplate -label "Enable Wind Oscillation" -addControl "windOscillation";

	editorTemplate -endLayout;

	// --------------------------------------------
	//          Other attributes
	// --------------------------------------------
	// Create an "Extras" section and also add controls for any
	// attributes we have not explicitly mentioned.
    editorTemplate -addExtraControls;

    editorTemplate -endScrollLayout;

	// Tell the attribute editor not to display the attributes we don't care about.
	editorTemplate -suppress "caching";
	editorTemplate -suppress "nodeState";
	editorTemplate -suppress "frozen";
	editorTemplate -suppress "inputMesh";
	editorTemplate -suppress "outputMesh";
	editorTemplate -suppress "currentFrame";
}


// --------------------------------------------
//     procs to define custom UI elements
// --------------------------------------------

// ----- slider for time step -----
global proc timeSliderAttributeNew( string $attrName )
{
	floatSliderGrp 
		-label "Time Step" 
		-field true
		-minValue 0.001 
		-fieldMinValue 0.00001
		-maxValue 0.1
		-fieldMaxValue 2
		-value 0.01
		timeSlider;
}

global proc timeSliderAttributeUpdate( string $attrName )
{
   floatSliderGrp -e -attribute $attrName timeSlider;
}

// ----- slider for solver iterations -----
global proc iterSliderAttributeNew( string $attrName )
{
	intSliderGrp 
		-label "Solver Iterations" 
		-field true
		-minValue 1 
		-maxValue 20
		-fieldMaxValue 100 
		-value 5
		iterSlider;
}

global proc iterSliderAttributeUpdate( string $attrName )
{
   intSliderGrp -e -attribute $attrName iterSlider;
}

// ----- slider for mass -----
global proc massSliderAttributeNew( string $attrName )
{
	floatSliderGrp 
		-label "Vertex Mass" 
		-field true
		-minValue 0.01 
		-fieldMinValue 0.0001
		-maxValue 10 
		-fieldMaxValue 500
		-value 1
		massSlider;
}

global proc massSliderAttributeUpdate( string $attrName )
{
   floatSliderGrp -e -attribute $attrName massSlider;
}


// ----- radio buttons for volumetric -----
global proc volumetricRadioAttributeNew( string $attrName)
{
	radioButtonGrp
		-label "Simulation Type"
		-numberOfRadioButtons 2
		-label1 "Volumetric"
		-data1 1
		-label2 "Thin-shell"
		-data2 0
		-select 1
		volumetricRadio;
}

global proc volumetricRadioAttributeUpdate( string $attrName )
{
   radioButtonGrp -e -attribute $attrName volumetricRadio;
}


// ----- slider for youngsModulus -----
global proc youngsModulusSliderAttributeNew( string $attrName )
{
	floatSliderGrp 
		-label "Young's Modulus" 
		-field true
		-minValue 1000
		-fieldMinValue 1
		-maxValue 10000
		-fieldMaxValue 1000000
		-value 5000
		youngsModulusSlider;
}

global proc youngsModulusSliderAttributeUpdate( string $attrName )
{
   floatSliderGrp -e -attribute $attrName youngsModulusSlider;
}


// ----- slider for poissonRatio -----
global proc poissonRatioSliderAttributeNew( string $attrName )
{
	floatSliderGrp 
		-label "Poisson Ratio" 
		-field true
		-minValue 0.1
		-fieldMinValue 0.01
		-maxValue 0.5
		-fieldMaxValue 1
		-value 0.3
		poissonRatioSlider;
}

global proc poissonRatioSliderAttributeUpdate( string $attrName )
{
   floatSliderGrp -e -attribute $attrName poissonRatioSlider;
}


// ----- slider for windMagnitude -----
global proc windMagnitudeSliderAttributeNew( string $attrName )
{
	floatSliderGrp 
		-label "Wind Magnitude" 
		-field true
		-minValue 0.1
		-fieldMinValue 0.001
		-maxValue 100
		-fieldMaxValue 1000
		-value 1
		windMagnitudeSlider;
}

global proc windMagnitudeSliderAttributeUpdate( string $attrName )
{
   floatSliderGrp -e -attribute $attrName windMagnitudeSlider;
}